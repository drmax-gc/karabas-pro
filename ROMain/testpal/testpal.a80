;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;;                   Test ROM for Karabas PRO                 ::
;;                         by Doctor Max                      ::
;;                           (c)2021                          ::
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
        DEVICE ZXSPECTRUM128

PORT_TURBO      EQU #028B
M_TURBO_OFF     EQU 0b00100000

                ORG #6000
start:          di
                ld sp,start
                call setIM2

                call RAM.setPixPage
                ld a,#00
                call RAM.fillPixPage

                call RAM.setAttrPage
                ld a,0b00000000
                call RAM.fillAttrPage

                call fillpall

                call print_logo

.loop           ld ix,274
                ei : halt

                ld hl,0

                ld b,36
                call delay_192t

                ld a,#7E
                ld c,a
.pal1_offs      EQU $+1
                ld hl,#4000
                ld e,#0E
                exx
                ld c,a
.pal2_offs      EQU $+1
                ld hl,#4000+32
                ld e,#0F
                exx

.ylp            ld b,(hl)       ;7t
                inc hl          ;6t
                ld a,e          ;4t
                or (hl)         ;7t
                exa             ;4t
                out (#FE),a     ;11t
                out (c),a       ;12t
                exa             ;4t
                out (#FE),a     ;11t
                out (c),a       ;12t
                exx             ;4t

                ld b,(hl)       ;7t
                inc hl          ;6t
                ld a,e          ;4t
                or (hl)         ;7t
                exa             ;4t
                inc hl          ;6t
                ld a,h          ;4t
                and #43         ;7t
                ld h,a          ;4t

                exx             ;4t

                inc hl          ;6t
                ld a,h          ;4t
                and #43         ;7t
                ld h,a          ;4t

                dec ix          ;10t
                ld a,hx         ;8t
                or lx           ;8t
                jp nz,.ylp      ;10t

                ld hl,(.pal1_offs)
                inc hl : inc hl
                ld a,h
                and #43
                ld h,a
                ld (.pal1_offs),hl

                ld hl,(.pal2_offs)
                inc hl : inc hl
                ld a,h
                and #43
                ld h,a
                ld (.pal2_offs),hl

                ld bc,PORT_TURBO
                ld a,M_TURBO_OFF
                out (c),a

                ;if ESC key pressed?
                ld a,#FE
                in a,(#FE)
                rrca
                jp c,.loop
                ld a,#F7
                in a,(#FE)
                rrca
                jp c,.loop

                ld bc,#FF7E
                ld a,#8F
                out (#FE),a
                out (c),a

                ld b,#FF
                ld a,#0E
                out (#FE),a
                out (c),a

                ld hl,paltest_fade_tbl
                push hl

                ld d,5
.fade_lp0       ld b,3
                ei : halt
                djnz $-2

                ld a,(hl) : inc hl
                ld e,(hl) : inc hl
                cpl : ld b,a
                ld a,#0F
                or e
                out (#FE),a
                out (c),a

                dec d
                jr nz,.fade_lp0

                pop hl

                ld d,5
.fade_lp1       ld b,2
                ei : halt
                djnz $-2

                ld a,(hl) : inc hl
                ld e,(hl) : inc hl
                cpl : ld b,a
                ld a,#0E
                or e
                out (#FE),a
                out (c),a

                xor a
                or e
                out (#FE),a

                dec d
                jr nz,.fade_lp1

                di : halt

paltest_fade_tbl
                DB 0b00000000,#00
                DB 0b00100100,#80
                DB 0b01001001,#00
                DB 0b01101101,#80
                DB 0b10010010,#00

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

; b - count
delay_192t:
;192-14=178t
.lp             DS 8,#E3                ;ex (sp),hl (19t each)
                                        ;19*8 = 152t
                                        ;178-152=26t
                cpi                     ;16t
                ld hl,0                 ;10t
                dec b                   ;4t
                jp nz,.lp               ;10t
                ret                     ;10t

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
setIM2:         ld hl,#7E00
                ld a,#7F
                ld (hl),a
                inc l
                jr nz,$-2
                inc h
                ld (hl),a
                ld a,#C9
                ld (#7F7F),a
                ld a,#7E
                ld i,a
                im 2
                ret

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
fillpall        ld hl,#4000
                ld e,#FF
                ld bc,640
.lp             ld a,r
                xor e
                sub c
                rrca
                xor e
                ld e,a
                sub l
                rlca
                sub e
                ld (hl),a
                inc hl
                ld a,e
                rrca
                xor l
                sub c
                rrca
                and 0b00000111
                ld (hl),a
                inc hl
                dec e
                dec bc
                ld a,b
                or c
                jr nz,.lp
                ret

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;; variables
screen_addr             WORD #A000
window_attr             BYTE 0b10111000
window_header_attr      BYTE #4F

;; current window vars
win_x           DB 0
win_y           DB 0
win_w           DB 64
win_h           DB 30
win_flags       DB 0
win_attr        DB 0
frm_attr        DB 0
window_addr     DW #A000
;;
cur_x           DB 0
cur_y           DB 0
;;
sym_attr        DB #47
bg_attr         DB 7
inv_attr        DB 0
linked_ptr      DW 0

                INCLUDE "ram.a80"
                INCLUDE "screen.a80"
                INCLUDE "print.a80"
                INCLUDE "logo.a80"
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

                ALIGN 256
font            INCBIN "./resourses/866p.out.fnt"

end:

                SAVESNA "testPAL.sna", start
                SAVEHOB "testPAL.$C","testPAL.C", start, end-start

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::


