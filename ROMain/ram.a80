;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;;                     ROMain for Karabas PRO                 ::
;;                         by Doctor Max                      ::
;;                         (c)2021-2023                       ::
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

CMR0            EQU #7FFD
CMR0_SEG0       EQU #01
CMR0_SEG1       EQU #02
CMR0_SEG2       EQU #04
CMR0_PLEKR      EQU #08
CMR0_ROM14      EQU #10
CMR0_BLOCK      EQU #20

CMR1            EQU #DFFD
CMR1_SEG3       EQU #01
CMR1_SEG4       EQU #02
CMR1_SEG5       EQU #04
CMR1_SCO        EQU #08
CMR1_NOROM      EQU #10
CMR1_CPM        EQU #20
CMR1_SCR        EQU #40
CMR1_80DS       EQU #80

        MODULE RAM
cmr0    DB 0
cmr1    DB 0

clearPixPage:
        xor a 
fillPixPage:
        ld hl,#8000
        ld de,#8001
        ld bc,#3FFF
        ld (hl),a
        ldir
        ret

clearAttrPage:
        xor a
fillAttrPage:
        ld hl,#C000
        ld de,#C001
        ld bc,#3FFF
        ld (hl),a
        ldir
        ret

; set pixel page (#06)
setPixPage:
        push af,bc
        ld bc,CMR1
        ld a,CMR1_80DS | CMR1_NOROM | CMR1_CPM | CMR1_SCR
        out (c),a
        ld (cmr1),a
        ld b,high CMR0
        ld a,CMR0_ROM14 | CMR0_PLEKR | 6
        out (c),a
        ld (cmr0),a
        pop bc,af
        ret

; set attr page (#3A)
setAttrPage:
        push af,bc
        ld bc,CMR1
        ld a,CMR1_80DS | CMR1_NOROM | CMR1_CPM | CMR1_SCR | 7
        out (c),a
        ld (cmr1),a
        ld b,high CMR0
        ld a,CMR0_ROM14 | CMR0_PLEKR | 2
        out (c),a
        ld (cmr0),a
        pop bc,af
        ret

setBootPage:
        push af,bc
        ld bc,CMR1
        ld a,CMR1_80DS | CMR1_NOROM | CMR1_CPM | 0
        out (c),a
        ld (cmr1),a
        ld b,high CMR0
        ld a,CMR0_ROM14 | CMR0_PLEKR | 7
        out (c),a
        ld (cmr0),a
        pop bc,af
        ret

;set page for FAT entries (#3B)
setFATPage:
        push af,bc
        ld bc,CMR1
        ld a,CMR1_80DS | CMR1_NOROM | CMR1_CPM | 7
        out (c),a
        ld (cmr1),a
        ld b,high CMR0
        ld a,CMR0_ROM14 | CMR0_PLEKR | 3
        out (c),a
        ld (cmr0),a
        pop bc,af
        ret
        ENDMODULE
