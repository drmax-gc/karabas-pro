;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;;                     ROMain for Karabas PRO                 ::
;;                         by Doctor Max                      ::
;;                         (c)2021-2023                       ::
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

        MODULE PALETTE
zx:     ld hl,zx_palette
;in: HL - palette
palset_333:
        ld c,#0F
.loop   ld e,(hl)
        inc hl
        ld d,(hl)
        inc hl
        call set_pal_cell_333
        dec c
        jp p,.loop
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;in: DE - color
;     C - cell
set_pal_cell_333:
        push bc
        ld a,e          ;color 332
        cpl             ;inverse bits
        ld b,a
        ld a,c          ;cell number
        ld c,#7E
        or d            ;extended bit B0
        out (#FE),a
        out (c),a
        pop bc
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
clear:  ld c,#0F
        ld de,0
.loop   call set_pal_cell_333
        dec c
        jp p,.loop
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
white:  ld c,#0F
        ld de,0b10010010
        jr clear.loop
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
check:  ld c,#7E
        ld b,0b11111111
        ld a,#00
        out (#FE),a
        out (c),a
        in a,(#FE)
        di : halt
        and #80
        xor #80
        jr z,.absend

        inc a
        ret

        ld a,#80
        out (#FE),a
        out (c),a
        in a,(#FE)


.absend
        ret

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
fadein: ld hl,palette
        ld de,BUFF
        ld bc,32
        ldir

        ld b,8
.loop   push bc

        ld hl,BUFF
        push hl
        pop ix
        ld b,16
.lp     ld e,(hl)
        inc hl
        ld d,(hl)
        inc hl
        call fadein_cell
        ld (ix),e
        inc ix
        ld (ix),d
        inc ix
        djnz .lp

        halt

        ld hl,BUFF
        call palset_333

        pop bc
        djnz .loop
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
fadeinout:
        call fadein

        ld b,8
.loop   push bc

        ld hl,BUFF
        push hl
        pop ix
        ld b,16
.lp     ld e,(hl)
        inc hl
        ld d,(hl)
        inc hl
        call fadeout_cell_from_bright_to_black
        ld (ix),e
        inc ix
        ld (ix),d
        inc ix
        djnz .lp

        halt

        ld hl,BUFF
        call palset_333

        pop bc
        djnz .loop
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
fadeout_cell_from_bright_to_black:
        ld c,0
        call get_r
        or a
        jr z,$+2+1
        dec a
        call put_r

        call get_g
        or a
        jr z,$+2+1
        dec a
        call put_g

        call get_b
        or a
        jr z,$+2+1
        dec a
        jr put_b

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
fadein_cell:
        ld c,0

        call get_r
        cp #07
        jr z,$+2+1
        inc a
        call put_r

        call get_g
        cp #07
        jr z,$+2+1
        inc a
        call put_g

        call get_b
        cp #07
        jr z,$+2+1
        inc a
        ;jr put_b
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;get B component
put_b:  rrca
        rr d
        and #03
        ld c,a
        ld a,e
        and 0b11111100
        or c
        ld e,a

        ld a,d
        and #80
        ld d,a
        ret
;get R component
put_r:  rlca : rlca
        ld c,a
        ld a,e
        and 0b11100011
        or c
        ld e,a
        ret
;put G component
put_g:  rrca : rrca : rrca
        ld c,a
        ld a,e
        and 0b00011111
        or c
        ld e,a
        ret
;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;in: DE - 333 palette cell
;get G component
get_g:  ld a,e
        rlca : rlca : rlca
        and #07
        ret
;get R component
get_r:  ld a,e
        rrca : rrca
        and #07
        ret
;get B component
get_b:  ld a,e
        rl d : rl a
        and #07
        ret

scroll_palbuff
        push bc
        ld hl,palbuff+2
        ld de,palbuff
        ld bc,30
        ldir
        pop bc
        ex de,hl
        ld (hl),c
        inc hl
        ld (hl),b
        ret

palbuff  DS 32

;;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;---+---+---+---+---+---+---+---++---
; 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0 || 7
;---+---+---+---+---+---+---+---++---
;g2 |g1 |g0 |r2 |r1 |r0 |b2 |b1 ||b0
;---+---+---+----+---+---+---+--++---

palette:
        DB      0b00000000,#00  ;0
        DB      0b00100110,#00  ;1
        DB      0b00010001,#00  ;2
        DB      0b01001110,#00  ;3
        DB      0b01100001,#00  ;4
        DB      0b01000010,#00  ;5
        DB      0b01010000,#00  ;6
        DB      0b10010010,#00  ;7

        DB      0b00100100,#00  ;8
        DB      0b00100111,#00  ;9
        DB      0b00011001,#00  ;A
        DB      0b00011010,#00  ;B
        DB      0b11001001,#00  ;C
        DB      0b01100011,#00  ;D
        DB      0b11011010,#00  ;E
        DB      0b11111011,#00  ;F

zx_palette:
        DB      0b00000000,#00  ;black
        DB      0b00000010,#00  ;blue
        DB      0b00010000,#00  ;red
        DB      0b00010010,#00  ;magenta
        DB      0b10000000,#00  ;green
        DB      0b10000010,#00  ;cyan
        DB      0b10010000,#00  ;yellow
        DB      0b10010010,#00  ;white

        DB      0b00000000,#00  ;black
        DB      0b00000011,#00  ;bright blue
        DB      0b00011000,#00  ;bright red
        DB      0b00011011,#00  ;bright magenta
        DB      0b11000000,#00  ;bright green
        DB      0b11000011,#00  ;bright cyan
        DB      0b11011000,#00  ;bright yellow
        DB      0b11011011,#00  ;bright white

        ENDMODULE

